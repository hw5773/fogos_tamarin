theory versatile
begin

builtins: diffie-hellman
functions: senc/2, sdec/2, sign/2, verify/3, pk/1, true/0, h/1
equations: sdec(senc(m, k), k) = m,
           verify(msg, sign(msg, k), pk(k)) = true

/*
 * Creating a platform
 * Certificate <type, agent, public key, endorsement>
 */

rule Create_Platform:
    [ Fr(~kp) ]
  --[ CreatePlatform($P, ~kp) ]->
    [ !PrivateKey('platform', $P, ~kp), !Certificate('platform', $P, pk(~kp), $P) ]

/*
 * Creating a device
 */
rule Create_Device:
    [ Fr(~ka) ]
  --[ CreateDevice($P, $A, ~ka) ]->
    [ !PrivateKey('device', $A, ~ka), 
      !Certificate('device', $A, pk(~ka), $P) 
    ]

/*
 * Trust relationship
 */
rule Established_Trust:
    [ 
      !Certificate('platform', $P, pk(~kp), $P), 
      !Certificate('device', $A, pk(~ka), $P)
    ]
  --[ EstablishedTrust($A, $P) ]->
    [ 
      !Trust($A, pk(~kp)), 
      !Trust($P, pk(~ka))
    ]

/*
 * Corruption: the private key is revealed
 * We assume the platform cannot be corrupted.
 */

rule Corrupt_Device:
    [ !PrivateKey('device', $A, key) ]
  --[ CorruptDevice($A), LtkCorrupted(key), CorruptLtkOf($A) ]->
    [ Out(key) ]

/* 
 * Protocol Execution
 */
rule Send_Request_A:
  let
    flexid = h(pk(kb))
    request_message = < A, P, flexid, ~qos >
    signature = sign(request_message, ka)
    out_msg = < request_message, signature >
  in
    [ 
      Fr(~qos),
      !Certificate('device', A, pk(ka), P),
      !Certificate('device', B, pk(kb), P)
    ]
  --[ 
      SendRequestMsg(A, P, flexid)
    ]->
    [ Out(out_msg) ]

rule Send_Response_P:
  let 
    request_message = < A, P, flexid, qos >
    in_msg = < request_message, req_signature >
    response_message = < A, B, flexid >
    resp_signature = sign(response_message, kp)
    out_msg = < response_message, resp_signature >
  in
    [
      In(in_msg),
      !Certificate('device', A, pk(ka), P),
      !Certificate('device', B, pk(kb), P),
      !Certificate('platform', P, pk(kp), P),
      !Trust(P, pk(ka))
    ]
  --[ 
      EQUAL(verify(request_message, req_signature, pk(ka)), true),
      ReceiveRequestMsg(A, P, flexid), 
      SendResponseMsg(P, A, B, flexid)
    ]->
    [ Out(out_msg) ]

rule Receive_Response_A:
  let 
    response_message = < A, B, flexid >
    resp_signature = sign(response_message, kp)
    in_msg = < response_message, resp_signature >
  in
    [ 
      In(in_msg), 
      !Certificate('platform', P, pk(kp), P),
      !Certificate('device', B, pk(kb), P),
      !Trust(A, pk(kp))
    ]
  --[
      EQUAL(verify(response_message, resp_signature, pk(kp)), true),
      ReceiveResponseMsg(P, A, B, flexid)
    ]->
    [ Connection(A, B, flexid) ]

rule Send_Hello_A:
  let
    cert = < A, sign(h(pk(ka)), kp) >
    initiator_hello = < 'g'^(~x), pk(ka), cert >
  in
    [
      Fr(~x),
      Connection(A, B, flexid),
      !PrivateKey('platform', P, kp),
      !Certificate('device', A, pk(ka), P)
    ]
  --[
      SendInitiatorHello(A, B, flexid, initiator_hello),
      HandshakeStart(flexid)
    ]->
    [ 
      Out(initiator_hello),
      InitiatorHello(A, B, flexid, ~x, initiator_hello)
    ]

rule Send_Hello_B:
  let
    initiator_hello = < gx, pk(ka), cert >
    responder_hello = < 'g'^(~y), pk(kb)>
    master_secret = gx^(~y)
    shared_secret = h(master_secret)
    transcript = senc(h(initiator_hello), shared_secret)
    signature = sign(transcript, kb)
    out_msg = < responder_hello, transcript, signature >
  in
    [
      Fr(~y),
      In(initiator_hello),
      InitiatorHello(A, B, flexid, ~x, initiator_hello),
      !Certificate('platform', P, pk(kp), P),
      !Certificate('device', A, pk(ka), P),
      !Certificate('device', B, pk(kb), P),
      !Trust(A, pk(kp)),
      !Trust(B, pk(kp))
    ]
  --[
      EQUAL(verify(h(pk(ka)), snd(cert), pk(kp)), true),
      VerifySignature(initiator_hello, P, A),
      MasterSecretEstablished(B, A, master_secret),
      SendResponderHello(B, A, flexid, ~x, ~y, initiator_hello, responder_hello)
    ]->
    [ 
      Out(out_msg),
      ResponderHello(B, A, flexid, ~x, ~y, initiator_hello, responder_hello)
    ]

rule Send_Finished_A:
  let
    in_transcript = initiator_hello
    out_transcript = < initiator_hello, responder_hello >
    master_secret = 'g'^x^y
    shared_secret = h(master_secret)
    initiator_finished = senc(h(out_transcript), shared_secret)
  in
    [
      In(< responder_hello, transcript, signature >),
      ResponderHello(B, A, flexid, x, y, initiator_hello, responder_hello),
      !Certificate('device', B, pk(kb), P)
    ]
  --[
      EQUAL(verify(transcript, signature, pk(kb)), true),
      EQUAL(sdec(transcript, shared_secret), h(in_transcript)),
      EQUAL(flexid, h(pk(kb))),
      VerifySignature(responder_hello, P, B),
      VerifyTranscript(A, in_transcript),
      MasterSecretEstablished(A, B, master_secret)
    ]->
    [
      Out(initiator_finished),
      InitiatorFinished(A, B, flexid, x, y, master_secret, initiator_hello, responder_hello, initiator_finished)
    ]

rule Receive_Finished_B:
  let
    transcript = < initiator_hello, responder_hello >
	  shared_secret = h(master_secret)
  in
    [
      In(initiator_finished),
      InitiatorFinished(A, B, flexid, x, y, master_secret, initiator_hello, responder_hello, initiator_finished)
    ]
  --[
      EQUAL(sdec(initiator_finished, shared_secret), h(transcript)),
      VerifyTranscript(B, transcript),
      SessionEstablished(flexid, A, B, master_secret),
      REACHABLE()
    ]->
    [
    ]

// ------------------------------ //
// ----------- Lemmas ----------- //
// ------------------------------ //

lemma platform_accountability:
  all-traces
  "
  All A B P id #ta.ReceiveResponseMsg(P, A, B, id)@ta ==>
  (Ex #tb #tc.(SendResponseMsg(P, A, B, id)@tb 
              & EstablishedTrust(A, P)@tc
              & (#tb < #ta)
              & (#tc < #tb)))
  "

lemma mutual_authentication:
  all-traces
  "
  All A B id k #ta.SessionEstablished(id, A, B, k)@ta ==>
  (Ex A B P m1 m2 kp ka kb #tb #tc #td #te #tf #tg #th #ti.
              SendRequestMsg(A, P, id)@tb
              & ReceiveResponseMsg(P, A, B, id)@tc
              & HandshakeStart(id)@td
              & VerifySignature(m1, P, B)@te
              & VerifySignature(m2, P, A)@tf
              & CreateDevice(P, A, ka)@tg
              & CreateDevice(P, B, kb)@th
              & CreatePlatform(P, kp)@ti
              & (#tb < #tc)
              & (#tc < #td)
              & (#td < #te)
              & (#td < #tf)
              & (#te < #ta)
              & (#tf < #ta))
  "

lemma channel_security:
  all-traces
  "
  All A B id k #ta.SessionEstablished(id, A, B, k)@ta ==>
  (Ex #tb #tc #td.
                HandshakeStart(id)@tb
                & MasterSecretEstablished(A, B, k)@tc
                & MasterSecretEstablished(B, A, k)@td
                & (#tb < #tc)
                & (#tb < #td)
                & (#tc < #ta)
                & (#td < #ta))
  "

lemma existence_reachability:
  exists-trace
  "
  Ex #t.REACHABLE()@t
  "

// ------------------------------ //
// -------- Restrictions -------- //
// ------------------------------ //

// Equality
restriction equal:
"
  All l r #t.
    EQUAL(l, r) @t ==> l = r
"

end
